grammar cmd

cmd        <- cmdlist / empty
cmdlist    <- cmdsingle (sep (";" / "&") sep cmdlist)?                                           %make_list
cmdsingle  <- cmdpipe  (sep ("||" / "&&") sep cmdsingle)?                                        %make_single
cmdpipe    <- cmdredir  (sep ("|" !"|") sep cmdpipe)?                                            %make_pipe
cmdredir   <- cmdargs ( sep (">>-" / ">>" / "<<" / "<>" / "<&" / ">&" / "<" / ">") sep arg )*    %make_redir
cmdargs    <- cmdbrac / args 
cmdbrac    <- "(" sep cmd sep ")"                                                                %make_cmdbrac
args       <- arg (" "+ arg)*                                                                    %make_args

arg        <-  arg_quot1 / arg_quot2 / arg_noquot / empty
arg_noempty <-  arg_quot1 / arg_quot2 / arg_noquot
arg_quot1  <-  "'" [^']* "'"                                  %make_arg_quot
arg_quot2  <-  '"' [^"]* '"'                                  %make_arg_quot
arg_noquot <-  [^ ;|&()"'><]+                                 %make_arg_noquot

empty      <-  ""?
sep        <- " "*
